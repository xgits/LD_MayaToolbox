global string $seamsEasySortValue = "";
global string $seamsEasySortOrder = "";

global proc AEseamsEasyTemplate (string $nodeName) {
	editorTemplate -beginScrollLayout ;

	editorTemplate -beginLayout "Seam Lines settings" -collapse 0;
		editorTemplate -label "Gap" -addControl "gap";
		editorTemplate -label "Thickness" -addControl "thickness" ;
		editorTemplate -label "Divisons" -addControl "divisions" ;
		editorTemplate -label "Extrude all boundaries" -addControl "extrudeAllBoundaries";
		editorTemplate -addSeparator;
		editorTemplate -label "Profile mode" -addControl "profileMode";
	editorTemplate -endLayout;

	source AEnewNonNumericMulti;
	editorTemplate -callCustom "AEseamsEasy_new" "AEseamsEasy_new" "offset";

	editorTemplate -beginLayout "Profile curve settings" -collapse 0;
		editorTemplate -label "Width" -addControl "profileWidth";
		editorTemplate -label "Depth" -addControl "profileDepth";
		editorTemplate -label "Subdivisions" -addControl "profileSubdivisions";
		AEaddRampControl( $nodeName + ".profileCurve" );
	editorTemplate -endLayout;

	editorTemplate -beginLayout "Hard edge settings" -collapse 0;
		editorTemplate -label "Threshold angle" -addControl "hardEdgeAngle";
	editorTemplate -endLayout;

	editorTemplate -suppress "inMesh";
	editorTemplate -suppress "outMesh";
	editorTemplate -suppress "seamLines";
	editorTemplate -suppress "distanceMultiplier";
	editorTemplate -suppress "depthMultiplier";
	
	editorTemplate -addExtraControls ;
	editorTemplate -endScrollLayout ;
}


global proc AEseamsEasy_new( string $name ){
	setUITemplate -pst attributeEditorTemplate;

	if(`frameLayout -q -ex AEseamsEasyPresetFrame`)
		deleteUI AEseamsEasyPresetFrame;
	
	frameLayout -l "Manual profile settings" -collapse 0 AEseamsEasyPresetFrame;
	setParent..;

	AEseamsEasy_buildUI($name);

	setUITemplate -ppt;
}

global proc AEseamsEasy_buildUI( string $name ){
	string $buffer[];
	tokenize $name "." $buffer;
	string $node = $buffer[0];

	button -l "Insert edge loop" -c ("AEnewNonNumericMultiAddNewItem(\""+$node+"\",\"offset\");") -p AEseamsEasyPresetFrame;
	
	int $offsetIndices[] = `getAttr -multiIndices $name`;

	if(size($offsetIndices)>0){
		// Collumn labels
		string $header = `formLayout -p AEseamsEasyPresetFrame`;

			string $distanceLabel = `iconTextButton
				-h 15
				-ann "Sort by Distance"
				-c ("AEseamsEasy_sort(\""+$node+"\", \"distance\")")
				-st "textOnly"
				-l "Distance"`;
			string $depthLabel = `iconTextButton
				-h 15
				-ann "Sort by Depth"
				-c ("AEseamsEasy_sort(\""+$node+"\", \"depth\")")
				-st "textOnly"
				-l "Depth"`;
			string $stitchesLabel = `text -l "Stitches"`;

		setParent..;

		formLayout -e
		-attachForm $distanceLabel "top" 6
		-attachForm $distanceLabel "left" 15

		-attachForm $depthLabel "top" 6
		-attachForm $depthLabel "left" 195

		-attachForm $stitchesLabel "top" 6
		-attachForm $stitchesLabel "left" 370
		$header;

		int $i=0;
		for($index in $offsetIndices){
			string $offsetEntry = `formLayout -p AEseamsEasyPresetFrame`;

				string $distanceGrp = `attrFieldSliderGrp
					-at ($name+"["+$index+"].distance")
					-w 180
					-cw3 0 60 120
					-co3 0 0 0`;

				string $depthGrp = `attrFieldSliderGrp
					-at ($name+"["+$index+"].depth")
					-w 180
					-cw3 0 60 120
					-co3 0 0 0`;

				string $stitchBox = `checkBox -l ""`;
				connectControl $stitchBox ($name+"["+$index+"].stitch");

				string $removeBtn = `symbolButton
				-i "cross.png"
				-w 20
				-c ("AEremoveMultiElement(\""+$name+"["+$index+"]"+"\")")`;

			setParent..;

			formLayout -e
				-attachForm $distanceGrp "top" 3
				-attachForm $distanceGrp "left" 0

				-attachForm $depthGrp "top" 3
				-attachControl $depthGrp "left" 0 $distanceGrp

				-attachForm $stitchBox "top" 5
				-attachControl $stitchBox "left" 10 $depthGrp

				-attachForm $removeBtn "top" 1
				-attachForm $removeBtn "right" 5

			$offsetEntry;

			if($i%2==0)
				control -e -bgc 0.25 0.25 0.25 $offsetEntry;

			$i++;
		}

		string $multiHeader = `formLayout -p AEseamsEasyPresetFrame`;

			string $distanceMultiLabel = `text
				-l "Distance multiplier"`;
			string $depthMultilabel = `text
				-l "Depth multiplier"`;

		setParent..;

		formLayout -e
		-attachForm $distanceMultiLabel "top" 6
		-attachForm $distanceMultiLabel "left" 15

		-attachForm $depthMultilabel "top" 6
		-attachForm $depthMultilabel "left" 195
		$multiHeader;

		string $multi = `formLayout -p AEseamsEasyPresetFrame  -bgc 0.25 0.25 0.25`;

				string $distanceMulti = `attrFieldSliderGrp
					-at ($node+".distanceMultiplier")
					-w 180
					-cw3 0 60 120
					-co3 0 0 0`;

				string $depthMulti = `attrFieldSliderGrp
					-at ($node+".depthMultiplier")
					-w 180
					-cw3 0 60 120
					-co3 0 0 0`;

		setParent..;

		formLayout -e
		-attachForm $distanceMulti "top" 6
		-attachForm $distanceMulti "left" 0

		-attachForm $depthMulti "top" 6
		-attachControl $depthMulti "left" 0 $distanceMulti
		$multi;

	}
}

global proc AEseamsEasy_sort(string $node, string $value){
	global string $seamsEasySortValue;
	global string $seamsEasySortOrder;

	$seamsEasySortOrder = ($seamsEasySortValue == $value && $seamsEasySortOrder=="asc") ? "desc" : "asc";
	$seamsEasySortValue = $value;

	seamsEasy -e -sort $seamsEasySortValue $seamsEasySortOrder $node;
}